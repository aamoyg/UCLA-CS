;       UCLA SPRING 2003
;       CS 33 PROJECT 6:  CONSTRUCTION PROJECT:
;                         SUPPORT SUBROUTINES 4
;       JOHN A. ROHR
;       JANUARY 20, 2003
;
        .EQU     N,6                     ; BRICK GRAB LIMIT
        .EQU     TRYLMT,10               ; MOVE  TRY  LIMIT

        .EQU     TIMBIN,$FF7             ; BINARY TIME LOCATION

        .EQU     DEBUG,0                 ; DEBUG CONTROL
        .EQU     ROWS,14                 ; DISPLAY ROWS
        .EQU     COLS,38                 ; DISPLAY COLUMNS

        .EQU     DROWS,3                 ; DATA     ROWS
        .EQU     BROWS,10                ; BUILDING ROWS
        .EQU     BCOLS,24                ; BUILDING COLUMNS
        .EQU     SROWS,10                ; BUILDING ROWS
        .EQU     SCOLS,12                ; BUILDING COLUMNS
        .EQU     MAXGRD,8                ; MAXIMUM  GIRDERS

        .EQU     BURPOS,14               ; INITIAL BUILDING ROW
        .EQU     BUCPOS,1                ; INITIAL BUILDING COLUMN
        .EQU     SURPOS,14               ; INITIAL SUPPLY   ROW
        .EQU     SUCPOS,27               ; INITIAL SUPPLY   COLUMN
        .EQU     CRRPOS,4                ; INITIAL CRANE    ROW
        .EQU     CRCPOS,27               ; INITIAL CRANE    COLUMN
        .EQU     CRICHR,'c'              ; INITIAL CRANE    CHAR

        .EQU     MOVERT,'M'              ; MOVE RIGHT CHARACTER
        .EQU     MOVELT,'K'              ; MOVE LEFT  CHARACTER
        .EQU     MOVEUP,'H'              ; MOVE UP    CHARACTER
        .EQU     MOVEDN,'P'              ; MOVE DOWN  CHARACTER

        .EQU     ENDPRG,'Q'              ; QUIT               CHAR
        .EQU     SHOCON,'C'              ; SHOW CONSTRUCTION  CHAR
        .EQU     SHOSOL,'S'              ; SHOW SOLUTION      CHAR
        .EQU     GRAB1,'1'               ; GRAB ONE BRICK     CHAR
        .EQU     GRABG,'G'               ; GRAB GIRDER INPUT  CHAR
        .EQU     GRABGC,'C'              ; GRAB GIRDER DISPLY CHAR
        .EQU     RELEAS,'R'              ; RELEASE LOAD       CHAR

        .EQU     TITPOS,0                ; TITLE          POSITION
        .EQU     STAPOS,TITPOS+COLS      ; STATUS         POSITION
        .EQU     SEPPOS,TITPOS+2*COLS    ; SEPARATOR      POSITION
        .EQU     MODPOS,STAPOS           ; MODE           POSITION
        .EQU     TIMPOS,STAPOS+10        ; TIME           POSITION
        .EQU     BRKPOS,STAPOS+21        ; BRICKS         POSITION
        .EQU     BRTPOS,STAPOS+25        ; BRICK TOTAL    POSITION
        .EQU     GRDPOS,STAPOS+35        ; GIRDERS        POSITION
        .EQU     GRTPOS,STAPOS+37        ; TOTAL GIRDERS  POSITION
        .EQU     ERRPOS,STAPOS+9         ; TERMINAL ERROR POSITION
        .EQU     TRYPOS,STAPOS+16        ; TOO MAY TRIES  POSITION
        .PAGE                    ;
        .EQU     CRT_BUF,$100    ; CRT DISPLAY I/O ADDRESS
        .EQU     CRT_XPOS,$314   ; CRT X POSITION  ADDRESS
        .EQU     CRT_YPOS,$315   ; CRT Y POSITION  ADDRESS
        .EQU     CRT_CTRL,$316   ; CRT CONTROL I/O ADDRESS

        .EQU     INIFUN,$FFC     ; I/O INITIALIZATION FUNCTION
        .EQU     KBDFUN,$FFD     ; I/O KEYBOARD       FUNCTION
        .EQU     TAPFUN,$FFE     ; I/O TAPE DRIVE     FUNCTION
        .EQU     CLKFUN,$FFF     ; I/O CLOCK          FUNCTION


        .EQU     CLEAR,1         ; CRT   CLEAR SCREEN OPCODE
        .EQU     BELLOP,6        ; CRT   RING BELL    OPCODE

        .EQU     TAPREW,0        ; TAPE  REWIND       OPCODE
        .EQU     TAPRD,1         ; TAPE  READ         OPCODE

        .EQU     CLKRES,1        ; CLOCK RESET OPCODE + 1
        .EQU     CLKGO,2         ; CLOCK START OPCODE + 1
        .EQU     CLKSTP,0        ; CLOCK STOP  OPCODE + 1


        .EQU     TERM,0          ; STRING TERMINATOR


        .EQU     ERROR,$B00      ; DISPLAY ERROR       SUBROTUINE
        .EQU     PUTNUM,$B01     ; DISPLAY NUMBER      SUBROUTINE
        .EQU     PTOA,$B02       ; POSITION-TO-ADDRESS FUNCTION

        .EQU     @,$A00          ; SUBROUTINE MODULE CODE ADDRESS

                                 ; TRANSFER TABLE
         JMP     SAVCON          ; SAVE    CONSTRUCTION  SUBROUTINE
         JMP     DISCON          ; DISPLAY CONSTRUCTION  SUBROUTINE
         JMP     DISSOL          ; DISPLAY SOLUTION      SUBROUTINE
         JMP     CKSOLN          ; CHECK   SOLUTION      FUNCTION
        .PAGE                    ; ******************************
                                 ; * SAVE CONSTRUCTION PROJECT  *
                                 ; ******************************
                                 ;
                                 ;
        .EQU     SVCNCH,2        ; PROJECT CHARACTER
        .PAGE                    ;
SAVCON: BGN#    3               ; INITIATE SUBROUTINE
         PSHA                    ; SAVE ACC
         PSHX                    ; SAVE XR

         LDX#    MODPOS          ; TEST FOR CONSTRUCTION DISPLAYED
         INB+    CRT_BUF         ;
         CMA#    'C'             ;
         JEQ     @+4             ; JUMP IF CONSTRUCTION DISPLAYED

         LDA#    11              ; SET ERROR 11
         JSR     ERROR           ;
         JMP     SAVCDN          ;

         CLR   ! R               ; R := 0

         LDA   ! R               ; TEST FOR MORE ROWS
         CMA#    ROWS-DROWS      ;
         JGE     SAVCDN          ; JUMP IF NO MORE ROWS

         CLR   ! C               ; C := 0

         LDA   ! C               ; TEST FOR MORE COLUMNS
         CMA#    SUCPOS-1        ;
         JLT     @+3             ; JUMP IF MORE COLUMNS

         INC   ! R               ; R := R + 1
         JMP     @-8             ;

         LDA   ! R               ; GET A PROJECT CHARACTER
         ADA#    DROWS+1         ;
         LDX   ! C               ;
         ADX#    1               ;
         JSR     PTOA            ;
         INB+    CRT_BUF         ;
         STA   ! SVCNCH          ; SAVE THE CHARACTER

         LDA   ! R               ; SAVE THE PROJECT CHARACTER
         MUL#    SUCPOS-1        ;
         ADA   ! C               ;
         TAX                     ;
         LDA   ! SVCNCH          ;
         STC     CBUF            ;

         INC   ! C               ; C := C + 1
         JMP     @-19            ;

SAVCDN: FIN#    3               ; TERMINATE SUBROUTINE
         POPX                    ; RESTORE XR
         POPA                    ; RESTORE ACC
         RTN                     ; RETURN
        .PAGE                    ; ********************************
                                 ; * DISPLAY CONSTRUCT'N PROJECT  *
                                 ; ********************************
                                 ;
                                 ;
        .EQU     DSCNCH,2        ; PROJECT CHARACTER
        .PAGE                    ;
DISCON: BGN#    3               ; INITIATE SUBROUTINE
         PSHA                    ; SAVE ACC
         PSHX                    ; SAVE XR

         CLR   ! R               ; R := 0

         LDA   ! R               ; TEST FOR MORE ROWS
         CMA#    ROWS-DROWS      ;
         JGE     DISCDN          ; JUMP IF NO MORE ROWS

         CLR   ! C               ; C := 0

         LDA   ! C               ; TEST FOR MORE COLUMNS
         CMA#    SUCPOS-1        ;
         JLT     @+3             ; JUMP IF MORE COLUMNS

         INC   ! R               ; R := R + 1
         JMP     @-8             ;

         LDA   ! R               ; GET A PROJECT CHARACTER
         MUL#    SUCPOS-1        ;
         ADA   ! C               ;
         TAX                     ;
         LDA   ! DSCNCH          ;
         LDC     CBUF            ;
         STA   ! DSCNCH          ; SAVE THE CHARACTER

         LDA   ! R               ; DISPLAY THE  PROJECT CHARACTER
         ADA#    DROWS+1         ;
         LDX   ! C               ;
         ADX#    1               ;
         JSR     PTOA            ;
         LDA   ! DSCNCH          ;
         OUTB+   CRT_BUF         ;

         INC   ! C               ; C := C + 1
         JMP     @-20            ;

DISCDN: LDX#    MODPOS          ; DISPLAY 'C' MODE
         LDA#    'C'             ;
         OUTB+   CRT_BUF         ;

         FIN#    3               ; TERMINATE SUBROUTINE
         POPX                    ; RESTORE XR
         POPA                    ; RESTORE ACC
         RTN                     ; RETURN
        .PAGE                    ; *************************
                                 ; * DISPLAY THE SOLUTION  *
                                 ; *************************
                                 ;
                                 ;
        .EQU     R,0             ; ROW
        .EQU     C,1             ; COLUMN
                                 ;
DISSOL: BGN#    2               ; INITIATE SUBROUTINE
         PSHA                    ; SAVE ACC
         PSHX                    ; SAVE XR

         LDA#    DROWS+1         ; CLEAR ABOVE THE SOLUTION
         STA   ! R               ;

         LDA   ! R               ;
         CMA#    BURPOS-BROWS    ;
         JGT     @+15            ;

         CLR   ! C               ;
         INC   ! C               ;

         LDA   ! C               ;
         CMA#    SUCPOS          ;
         JGE     @+8             ;

         LDA   ! R               ;
         LDX   ! C               ;
         JSR     PTOA            ;
         LDA#    ' '             ;
         OUTB+   CRT_BUF         ;
         INC   ! C               ;
         JMP     @-9             ;
         INC   ! R               ;
         JMP     @-16            ;
        .PAGE
         CLR   ! R               ; DISPLAY SOLUTION ROWS

DISSLP: LDA   ! R               ;
         CMA#    BROWS           ;
         JGE     DISSBT          ;

         CLR   ! C               ; CLEAR TO THE LEFT
         INC   ! C               ;

         LDA   ! C               ;
         CMA#    BUCPOS          ;
         JGE     @+9             ;

         LDA   ! R               ;
         ADA#    BURPOS-BROWS+1  ;
         LDX   ! C               ;
         JSR     PTOA            ;
         LDA#    ' '             ;
         OUTB+   CRT_BUF         ;
         INC   ! C               ;
         JMP     @-10            ;

         CLR   ! C               ; DISPLAY A SOLUTION ROW

         LDA   ! C               ;
         CMA#    BCOLS           ;
         JGE     @+16            ;

         LDA   ! R               ;
         ADA#    BURPOS-BROWS+1  ;
         LDX   ! C               ;
         ADX#    BUCPOS          ;
         JSR     PTOA            ;

         PSHX                    ;
         LDA   ! R               ;
         MUL#    BCOLS           ;
         ADA   ! C               ;
         TAX                     ;
         LDC     SBUF            ;
         POPX                    ;
         OUTB+   CRT_BUF         ;
         INC   ! C               ;
         JMP     @-17            ;
        .PAGE                    ;
         LDA#    BUCPOS+BCOLS    ; CLEAR TO THE RIGHT
         STA   ! C               ;

         LDA   ! C               ;
         CMA#    SUCPOS          ;
         JGE     @+9             ;

         LDA   ! R               ;
         ADA#    BURPOS-BROWS+1  ;
         LDX   ! C               ;
         JSR     PTOA            ;
         LDA#    ' '             ;
         OUTB+   CRT_BUF         ;
         INC   ! C               ;
         JMP     @-10            ;

         INC   ! R               ;
         JMP     DISSLP          ;

DISSBT: LDA#    BURPOS+1        ; CLEAR BELOW SOLUTION
         STA   ! R               ;

         LDA   ! R               ;
         CMA#    ROWS            ;
         JGT     @+15            ;

         CLR   ! C               ;
         INC   ! C               ;

         LDA   ! C               ;
         CMA#    SUCPOS          ;
         JGE     @+8             ;

         LDA   ! R               ;
         LDX   ! C               ;
         JSR     PTOA            ;
         LDA#    ' '             ;
         OUTB+   CRT_BUF         ;
         INC   ! C               ;
         JMP     @-9             ;
         INC   ! R               ;
         JMP     @-16            ;

         LDA#    'S'             ; SET SOLUTION MODE
         OUTB    CRT_BUF+MODPOS  ;

DISSDN: FIN#    2               ; TERMINATE SUBROUTINE
         POPX                    ; RESTORE XR
         POPA                    ; RESTORE ACC
         RTN                     ; RETURN
        .PAGE                    ; ***************************
                                 ; * CHECK THE SOLUTION      *
                                 ; *                         *
                                 ; * RESULT:  0: NO SOLUTION *
                                 ; *          1: OK SOLUTION *
                                 ; *                         *
                                 ; ***************************
                                 ;
                                 ;
        .EQU     I,0             ; ROW
        .EQU     J,1             ; COLUMN
        .EQU     RESULT,2        ; RESULT
        .EQU     CKSNCH,3        ; SOLUTION CHARACTER
        .PAGE                    ;
CKSOLN: BGN#    4               ; INITIATE SUBROUTINE
         PSHX                    ; SAVE XR

         CLR   ! RESULT          ; RESULT := 0

         LDX#    MODPOS          ; TEST FOR PROJECT DISPLAYED
         INB+    CRT_BUF         ;
         CMA#    'C'             ;
         JNE     CKSEND          ; JUMP IF NOT DISPLAYED

         CLR   ! I               ; I := 0

         CLR   ! J               ; J := 0

         LDA   ! I               ; GET SOLUTION CHARACTER
         MUL#    BCOLS           ;
         ADA   ! J               ;
         TAX                     ;
         LDC     SBUF            ;

         CMA#    '*'             ; TEST FOR WILD CARD
         JEQ     @+10            ; JUMP IF  WILD CARD

         STA   ! CKSNCH          ; SAVE SOLUTION CHARACTER

         LDA   ! I               ; GET PROJECT CHARACTER
         ADA#    BURPOS-BROWS+1  ;
         LDX   ! J               ;
         ADX#    BUCPOS          ;
         JSR     PTOA            ;
         INB+    CRT_BUF         ;
         CMA   ! CKSNCH          ; CHECK FOR MATCH
         JNE     CKSEND          ; JUMP  IF  NO MATCH

         INC   ! J               ; J := J + 1
         LDA   ! J               ; TEST FOR DONE
         CMA#    BCOLS           ;
         JLT     @-19            ; JUMP IF NOT DONE

         INC   ! I               ; I := I + 1
         LDA   ! I               ; TEST FOR DONE
         CMA#    BROWS           ;
         JLT     @-24            ; JUMP IF NOT DONE

         INC   ! RESULT          ; RESULT := 1

CKSEND: LDA   ! RESULT          ; SET RESULT
         FIN#    4               ; TERMINATE SUBROUTINE
         POPX                    ; RESTORE XR
         RTN                     ; RETURN
        .PAGE                    ;
        .EQU     @,$B80           ; CONSTANT DATA BASE

TITLE: .BLKW    13               ;
        .EQU     TITLE_LENGTH,38  ;

STATUS:.BLKW    13               ;
        .EQU     STATUS_LENGTH,38 ;

SEPLIN:.BLKW    13               ;
        .EQU     SEPLIN_LENGTH,38 ;

AGAIN: .BLKW    13               ;
        .EQU     AGAIN_LENGTH,38  ;

OVER:  .BLKW    13               ;
        .EQU     OVER_LENGTH,38   ;

RINVST:.BLKW    13               ;
        .EQU     RINVST_LENGTH,38 ;

SINVST:.BLKW    13               ;
        .EQU     SINVST_LENGTH,38 ;

ERRMSG:.BLKW    13               ;
        .EQU     ERRMSG_LENGTH,38 ;

TRYMSG:.BLKW    13               ;
        .EQU     TRYMSG_LENGTH,38 ;

BLANKS:.BLKW    1                ;
        .PAGE
        .EQU     @,$C00          ; VARIABLE DATA BASE

DBGCTL:.BLKW    1               ; DEBUG CONTROL
TIMER: .BLKW    1               ; LOCAL PROJECT TIME
PHASE: .BLKW    1               ; PROGRAM PHASE
ACTION:.BLKW    1               ; ACTION INPUI
MORE:  .BLKW    1               ; MORE   ANSWER
ERRIND:.BLKW    1               ; ERROR INDICATOR
TRIES: .BLKW    1               ; BLOCKED MOVE TRIES
SLINE: .BLKW    1               ; SOLUTION LINE

GIRDL: .BLKW    1               ; LEFT  GIRDER SEGMENTS
GIRDR: .BLKW    1               ; RIGHT GRIDER SEGMENTS
TBRICK:.BLKW    1               ; TOTAL   BRICK  COUNT
TGIRD: .BLKW    1               ; TOTAL   GIRDER COUNT
CBRICK:.BLKW    1               ; CURRENT BRICK  COUNT
CGIRD: .BLKW    1               ; CURRENT GIRDER COUNT
SBRICK:.BLKW    1               ; SUPPLY  BRICK  COUNT
SGIRD: .BLKW    1               ; SUPPLY  GIRDER COUNT

NEXT:  .BLKW    1               ; NEXT SUPPLY TIME
READY: .BLKW    1               ; NEXT SUPPLY READY
INTYPE:.BLKW    1               ; INPUT TYPE
INTIME:.BLKW    1               ; INPUT TIME
INCNT: .BLKW    1               ; INPUT ITEM   COUNT
INBRIK:.BLKW    1               ; INPUT BRICK  COUNT
INGIRD:.BLKW    1               ; INPUT GIRDER COUNT
INLEN: .BLKW    1               ; INPUT GIRDER LENGTH

CRANE: .BLKW    1               ; CRANE CHARACTER
CRNROW:.BLKW    1               ; CRANE ROW
CRNCOL:.BLKW    1               ; CRANE COLUMN
PICKUP:.BLKW    1               ; LOAD PICKUP AREA
M:     .BLKW    1               ; CRANE BRICK LIFT COUNT
SCNLST:.BLKW    1               ; LAST INPUT SCAN CHARACTER
DECDIG:.BLKW    1               ; DECIMAL DIGIT VALUE
BUFADR:.BLKW    1               ; INPUT BUFFER ADDRESS

SGIRDL:.BLKW    MAXGRD          ; SUPPLY GIRDER LENGTHS

        .BLKW    8               ; FILL

SBUF:  .BLKW    (BROWS*BCOLS+2)/3             ; SOLUTION BUFFER

CBUF:  .BLKW    ((ROWS-DROWS)*(SUCPOS-1)+2)/3 ; CONSTRUCT BUFFER

DATEND:.BLKW    1               ;
        .PAGE                    ;
        .END                     ;
